
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.jfrog.bintray'

version = rootProject.version
group = rootProject.group
description = 'User Acceptance Testing artifact for deploydb'

mainClassName = 'uat.UatMainApp'

dependencies {
    compile ('org.codehaus.groovy:groovy-all:2.4.0+')

    [
            'dropwizard-core',
            'dropwizard-assets',
            'dropwizard-jersey',
            'dropwizard-views-mustache',
            'dropwizard-testing',
    ].each {
        compile ("io.dropwizard:${it}:${dropwizardVersion}")
    }

    /* Used instead of the default HTTP connector for the Jersey client to
     * allow us to make PATCH requests
     */
    compile ('org.glassfish.jersey.connectors:jersey-apache-connector:2.+')
    compile('com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.4.1')
    compile ('org.spockframework:spock-core:0.7-groovy-2.0')
    compile project(':dropwizard-integtest')
}

repositories {
    jcenter()
}

shadowJar {
    exclude 'META-INF/*.RSA', 'META-INF/*.DSA'
    manifest {
        attributes 'Main-Class' : mainClassName
    }
}

assemble.dependsOn shadowJar

////////////////////////////////////////////////////////////////////////////////
// PUBLISHING TASKS
////////////////////////////////////////////////////////////////////////////////

artifacts {
    archives distShadowTar, distShadowZip, shadowJar
}
/* Disabling the addition of a jar to the archives which gets in the way of our
 * means to publish artifacts. It appears that the java plugin doesn't give you
 * the means to remove the 'default jar' from the artifacts list by any other
 * means
 */
jar.enabled = false
configurations.archives.artifacts.removeAll { it.archiveTask.is jar }

bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        userOrg = 'lookout'
        repo = 'systems'
        name = 'DeployDB'
        labels = []

        version {
            name = project.version
            vcsTag = "v${project.version}"
            desc = project.description
        }
    }
}
bintrayUpload.dependsOn assemble